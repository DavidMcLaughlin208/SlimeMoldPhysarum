#version 440

struct Particle {
	vec4 pos;
	vec4 speciesMask;
};

struct Cell {
	vec4 val;
};

uniform int screenWidth;
uniform int screenHeight;
uniform float senseAngle;
uniform float steerStrength;
uniform float senseDistance;
uniform float speed;
uniform float maxTrailDensity;
uniform int sensorSize;
uniform bool speedAffectedByTrailDensity;
uniform float deltaTime;

layout(binding = 0) buffer particle {
	Particle particles[];
};

layout(binding = 1) buffer oldTrail {
	Cell oldTrailMap[];
};

layout(binding = 2) buffer newTrail {
	Cell newTrailMap[];
};

float scaleSpeed(float speed, float scalar) {
	return speed * (scalar / (maxTrailDensity / 4));
}

// Hash function www.cs.ubc.ca/~rbridson/docs/schechter-sca08-turbulence.pdf
uint hash(uint state)
{
	state ^= 2747636419u;
	state *= 2654435769u;
	state ^= state >> 16;
	state *= 2654435769u;
	state ^= state >> 16;
	state *= 2654435769u;
	return state;
}

float scaleToRange01(uint state)
{
	return state / 4294967295.0;
}

float sense(Particle particle, float angleOffset) {
	float angle = mod(particle.pos.z + angleOffset + 360.0, 360.0);
	vec2 direction = vec2(cos(angle), sin(angle));
	vec2 senseLocation = senseDistance * direction + particle.pos.xy;
	float sum = 0;
	vec4 senseWeight = particle.speciesMask * 2 - 1;
	for (int modX = -sensorSize; modX <= sensorSize; modX++) {
		int targetX = int(senseLocation.x) + modX;
		if (targetX < 0 || targetX > screenWidth - 1) {
			continue;
		}
		for (int modY = -sensorSize; modY <= sensorSize; modY++) {
			int targetY = int(senseLocation.y) + modY;
			if (targetY < 0 || targetY > screenHeight - 1) {
				continue;
			}
			int index = targetX + (targetY * screenWidth);
			sum += dot(senseWeight, oldTrailMap[index].val);	
		}
	}
	int sensorArea = sensorSize == 0 ? 1 : (sensorSize + 2) * (sensorSize + 2);
	return sum / (maxTrailDensity * sensorArea);
}


layout(local_size_x = 1024, local_size_y = 1, local_size_z = 1) in;
void main() {
	// Move Particles
	Particle particle = particles[gl_GlobalInvocationID.x];
	float left = sense(particle, -senseAngle);
	float right = sense(particle, senseAngle);
	float straight = sense(particle, 0.0);

	float randomness = scaleToRange01(hash(uint(particle.pos.x + particle.pos.y * screenWidth)));

	float angle = particles[gl_GlobalInvocationID.x].pos.z;
	float localSpeed = speed;

	if (straight > left && straight > right) {
		//particles[gl_GlobalInvocationID.x].pos.z -= steerStrength;
		if (speedAffectedByTrailDensity) {
			localSpeed = scaleSpeed(localSpeed, straight);
		}
		
	}
	else if (straight < left && straight < right) {
		angle = mod(angle + (randomness - 0.5) * 2 * steerStrength * deltaTime + 360.0, 360.0);
		if (speedAffectedByTrailDensity) {
			localSpeed = scaleSpeed(localSpeed, (left + right) / 2);
		}
	}
	else if (left > straight && left > right) {
		angle = mod(angle - (randomness * steerStrength * deltaTime + 360.0), 360.0);
		if (speedAffectedByTrailDensity) {
			localSpeed = scaleSpeed(localSpeed, left);
		}
	}
	else if (right > straight && right > left) {
		angle = mod(angle + (randomness * steerStrength * deltaTime + 360.0), 360.0);
		if (speedAffectedByTrailDensity) {
			localSpeed = scaleSpeed(localSpeed, right);
		}
	}
	else {
		if (speedAffectedByTrailDensity) {
			localSpeed = scaleSpeed(localSpeed, 0.1);
		}
	}
	vec2 direction = vec2(cos(angle), sin(angle));
	vec2 newPos = particle.pos.xy + direction * localSpeed * deltaTime;
	if (newPos.x < 0 || newPos.x > screenWidth - 1 || newPos.y < 0 || newPos.y > screenHeight - 1) {
		newPos.x = min(screenWidth - 1, max(0, particle.pos.x));
		newPos.y = min(screenHeight - 1, max(0, particle.pos.y));
		angle = mod(angle + 60, 360.0f);
		//particles[gl_GlobalInvocationID.x].pos.z = mod((particles[gl_GlobalInvocationID.x].pos.z + 60), 360.0f);
		//particles[gl_GlobalInvocationID.x].pos.z = rand(gl_GlobalInvocationID.xy) * 360.0;
	}
	particles[gl_GlobalInvocationID.x].pos = vec4(newPos.x, newPos.y, angle, 0);
	
	int index = int(newPos.x) + int(int(newPos.y) * screenWidth);

	// Modify Trail
	vec4 val = oldTrailMap[index].val;
	oldTrailMap[index].val = vec4(min(val.r + 1 * particle.speciesMask.r, maxTrailDensity), min(val.g + 1 * particle.speciesMask.g, maxTrailDensity), min(val.b + 1 * particle.speciesMask.b, maxTrailDensity), min(val.a + 1 * particle.speciesMask.a, maxTrailDensity));

}

